## This model was adapted from section 14.5 of Korner-Nievergelt et al
## 2015. It is a Cormack-Jolly-Seber model.

## We use the "ones" trick below to add log probabilities to the
## accumulator. This means passing a vector or matrix of 1's as data, then
## assigning a bernoulli likelihood. Further, to get the 'chi' variables a
## little bit of index trickery was used. The Stan model uses a while loop
## but that didnt work here so I passed an index in.  Another difference is
## I calculated the 'last' quanities in R and pass them in.

## Last updated on 3/29/2016
model {
    ## priors
    for(i in 1:4) {
        b0[i]~dnorm(0,1/5^2);
        b1[i]~dnorm(0,1/5^2);
    }
    for(v in 1:(K-1)) {a[v]~dnorm(0, 1/5^2);}
    a1~dnorm(0,1/5^2);
    sigmaphi~dt(0,1,2)I(0,5);
    sigmayearphi~dt(0,1,2)I(0,3);
    sigmap~dt(0,1,2)I(0,5);

    ## random effects; non-centered
    for(g in 1:nfam){
        fameffphi[g]~dnorm(0,1/sigmaphi^2);
        fameffp[g]~dnorm(0,1/sigmap^2);
    }
    for(ye in 1:4){
        yeareffphi[ye]~dnorm(0,1/sigmayearphi^2);
    }

    ## derived parameters
    for(i in 1:I){ ## loop over each individual
        ## calculate phi as a function of fixed and random effects
        for(t in 1:(K-1)) {
            logit(phi[i,t]) <- a[t]+ a1*carez[i]+ yeareffphi[year[i]]+ fameffphi[family[i]];
        }
        ## calculate p as a function of fixed and random effects
        p[i,1] <- 1;  ## first occasion is marking occasion
        for(t in 2:K){
            logit(p[i,t]) <- b0[year[i]] + b1[year[i]]*agec[t]+ fameffp[family[i]];
        }
    }
    ## probabilitiy of never being seen after last observation. ind here is
    ## a reverse index so this loop goes from K:2, recursively calculating
    ## backward.
    for(i in 1:I){
        chi[i,K+1] <- 1.0;
        for(j in ind){
            chi[i,j] <- (1-phi[i,j-1]) + phi[i,j-1]*(1-p[i,j])*chi[i,j+1];
        }
        chi[i,1] <- (1 - p[i,1]) * chi[i,2];
   }

    ## calculate likelihood of data
    for(i in 1:I){
        ## probability of survival, known alive since k<last
	for(t in 2:last[i]){
            ones[i,t]~dbern(phi[i,t-1])
	}
	## probability of observation given known alive
	for(t in 1:last[i]){
            CH[i,t]~dbern(p[i,t])
	}
	## probability of no observations after time period last
	ones2[i]~dbern(chi[i,last[i]+1])
    }
}

